apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.lm.ll.spark"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 1
        versionName "1.19.1022"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    //如果没有此设置，在Generate Signed Apk时会报“Could not download lint-checks.jar”等错误
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'GoogleAppIndexingWarning'  //禁用AndroidManifest.xml中的GoogleAppIndexingWarning
    }
    compileOptions {
        sourceCompatibility = "1.8"
        targetCompatibility = "1.8"
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildToolsVersion = '29.0.2'

//    splits {
//        abi {
//            enable true
//            reset()
//            include "armeabi-v7a","arm64-v8a"
//            universalApk true
//        }
//    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.drawerlayout:drawerlayout:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    implementation 'org.jsoup:jsoup:1.12.1'

    //注意：如果是Java工程则使用kotlinx-coroutines-core
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    //HanLP库 繁简转换
    //因为从maven中加载的jar中，某些字会被不正确的转换（如猛转换成勐，富豪转换成沃尔沃），所以不再通过gradle加载，而是导入修改词库后的jar包
    //implementation 'com.hankcs:hanlp:portable-1.7.1'

    //opencc-android 繁简转换库 （此库转换效果比较满意，但是有时会导致程序FC）
    //implementation 'com.zqc.opencc.android.lib:lib-opencc-android:0.8.0@aar'

    //图文混排库
    implementation('com.github.sendtion:XRichText:1.5') {
        exclude group: 'com.android.support'
    }

    //google风格进度条
    implementation 'com.github.castorflex.smoothprogressbar:library:1.1.0'

    //带有加载效果的TextView和ImageView控件
    implementation 'com.elyeproj.libraries:loaderviewlibrary:1.5.0'

    //objectbox
    debugImplementation "io.objectbox:objectbox-android-objectbrowser:$objectbox_version"
    releaseImplementation "io.objectbox:objectbox-android:$objectbox_version"
    implementation "io.objectbox:objectbox-kotlin:$objectbox_version"

    //ReactiveX series
    implementation "io.reactivex.rxjava2:rxkotlin:$rxkotlin_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid_version"

    //okhttp3 interceptor
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.2'

    //Retrofit series
    implementation "com.squareup.retrofit2:retrofit:$retrofit2_version"

    // RxJava2支持
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit2_version"

    // 字符串依赖
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit2_version"

    //gson依赖
    implementation "com.squareup.retrofit2:converter-gson:$retrofit2_version"

    //使用Delegate设计模式的RecyclerView.Adapter框架
    implementation 'com.hannesdorfmann:adapterdelegates3:3.1.0'

    //Automatic binding+disposal of RxJava 2 streams.
    implementation "com.uber.autodispose:autodispose-android:$uber_autodispose_version"
    implementation "com.uber.autodispose:autodispose-android-archcomponents:$uber_autodispose_version"
    implementation "com.uber.autodispose:autodispose-android-archcomponents-test:$uber_autodispose_version"

    //圆形ImageView控件
    implementation 'de.hdodenhof:circleimageview:3.0.1'

    //material风格对话框控件
    implementation 'com.afollestad.material-dialogs:core:2.0.0-alpha04'

    //Firebase核心组件（Analytics功能）
    implementation 'com.google.firebase:firebase-core:16.0.9'

    //Firebase的Crashlytics组件
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.0'

    //OkHttp3的Cookie持久化组件
    implementation 'com.github.franmontiel:PersistentCookieJar:v1.0.1'

    //关于消息页面
    implementation 'com.github.medyo:android-about-page:1.2.4'
    implementation files('libs/hanlp-portable-1.7.1.jar')
}

//这个要放在dependencies块的后面，不然build时会报"More than one file was found with OS independent path 'lib/armeabi-v7a/libobjectbox.so'"错误
apply plugin: 'io.objectbox'

apply plugin: 'kotlin-noarg'
apply plugin: 'kotlin-allopen'

apply plugin: 'com.google.gms.google-services'

noArg {
    annotation("com.lm.ll.spark.annotation.Poko")
}

allOpen {
    annotation("com.lm.ll.spark.annotation.Poko")
}
